CREATE OR REPLACE PACKAGE string_tracker
/*
Within a particular section of my application, I need to keep
track of whether a certain name has already been used.

Write a utility package that provides the following functionality:

* Record a name/string as being used.
* Tell me if a particular name is already in use.
* Reset my list.
* Display the list of used names.
*/
IS
   SUBTYPE variable_name_t IS VARCHAR2 (32767);

   PROCEDURE clear_used_list;

   FUNCTION string_in_use (value_in IN variable_name_t)
      RETURN BOOLEAN;

   PROCEDURE mark_as_used (value_in IN variable_name_t);
END string_tracker;
/

CREATE OR REPLACE PACKAGE BODY string_tracker
IS
   TYPE used_aat IS TABLE OF variable_name_t
      INDEX BY PLS_INTEGER;

   g_names_used   used_aat;

   PROCEDURE clear_used_list
   IS
   BEGIN
      g_names_used.DELETE;
   END clear_used_list;

   PROCEDURE mark_as_used (value_in IN variable_name_t)
   IS
   BEGIN
      g_names_used (g_names_used.COUNT + 1) := value_in;
   END mark_as_used;
   
   FUNCTION string_in_use (value_in IN variable_name_t)
      RETURN BOOLEAN
   IS
      c_count   CONSTANT PLS_INTEGER := g_names_used.COUNT;
      l_index            PLS_INTEGER := g_names_used.FIRST;
      l_found            BOOLEAN     DEFAULT FALSE;
   BEGIN
      WHILE (NOT l_found AND l_index <= c_count)
      LOOP
         l_found := value_in = g_names_used (l_index);
         l_index := l_index + 1;
      END LOOP;

      RETURN l_found;
   END string_in_use;

END string_tracker;
/